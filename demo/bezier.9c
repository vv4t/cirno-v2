#include <sdl>
#include <math>

fn linear_interp(class vec2 a, class vec2 b, f32 t) : class vec2
{
  return a.copy().add(b.copy().sub(a).mulf(t));
}

fn bezier_interp_R(class vec2[] curve, i32 i, i32 n, f32 t) : class vec2
{
  if (n == 1) {
    return linear_interp(curve[i], curve[i + 1], t);
  } else if (n == 2) {
    class vec2 a = linear_interp(curve[i], curve[i + 1], t);
    class vec2 b = linear_interp(curve[i + 1], curve[i + 2], t);
    
    return linear_interp(a, b, t);
  } else {
    i32 n_a = n / 2;
    i32 n_b = n - n_a;
    
    class vec2 a = bezier_interp_R(curve, i, n_a + 1, t);
    class vec2 b = bezier_interp_R(curve, i + n_a, n_b, t);
    
    return linear_interp(a, b, t);
  }
}

fn bezier_interp(class vec2[] curve, f32 t) : class vec2
{
  return bezier_interp_R(curve, 0, curve.length - 1, t);
}

class vec2[] main_curve = array_init<class vec2> {
  new vec2(10, 10),
  new vec2(500, 10),
  new vec2(500, 100),
  new vec2(10, 100),
  new vec2(10, 200),
  new vec2(200, 200),
  new vec2(200, 250)
};

fn draw_curve(class vec2[] curve)
{
  i32 N = 36 - 1;
  f32 step = 1.0 / (N + 1);
  
  for (i32 i = 0; i < N; i++) {
    class vec2 a = bezier_interp(curve, i * step);
    class vec2 b = bezier_interp(curve, (i + 1) * step);
    
    draw_line(a.x, a.y, b.x, b.y);
  }
}

f32 time = 0;

fn update()
{
  time += 0.0005;
}

fn draw()
{
  draw_curve(main_curve);
}
